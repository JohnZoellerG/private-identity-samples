plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://androidx.dev/snapshots/latest/artifacts/repository' }
}

android {
    namespace 'com.google.webkit.webviewsample'
    compileSdk 35

    signingConfigs {
        debug {
            def keystoreFile = project.rootProject.file('debug.keystore')
            if (keystoreFile.exists()) {
                storeFile file(keystoreFile.toPath())
            } else {
                Properties properties = new Properties()
                def propertiesFile = project.rootProject.file('local.properties')
                if (propertiesFile.exists()) {
                    properties.load(propertiesFile.newDataInputStream())
                    storeFile file(properties.getProperty("key_path"))
                }
            }
        }
    }

    defaultConfig {
        applicationId "com.google.webkit.webviewsample"
        minSdk 29
        targetSdk 34
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

    implementation 'androidx.credentials:credentials-play-services-auth:1.5.0-rc01'
    implementation 'androidx.credentials:credentials:1.5.0-rc01'

    implementation("androidx.webkit:webkit:1.12.1")
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.1.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}